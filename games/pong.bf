
{ layout:
  run  b_s    _  _      _  b_vx  _  b_vy
  _    b_px   _  b_py   _  b_l   _  b_l
  _    pl_px  _  pl_py  _  p_lx  _  p_ly
  _    pr_px  _  pr_py  _  p_lx  _  p_ly
  _    _      1  _      _  _     _  _
}


{ INITIALIZE }

+                                                     { b_s }
[>+ >>>>+ >>+ << <<<< <-]+ >>>>>>>>
>>--[<+>>+<--]<+ >>+ <<<
++++                                                  { b_l }
[>- >>- >>++ >>++ << << << <--] >>>>>>>>
>--[<+>--]<+ [>>>+ >>>>>>>>+ <<<<<<<< <<<-]
>++++++++[<++++>-]<                                   { p_ly }
[>>>- >>>>++ >>>>- >>>>++ <<<< <<<< <<<< <<<--]
>++++[<++++>-]<                                       { w }
[>+ >>>>>>>>- <<<<<<<< <-]
++++                                                  { p_lx }
[>- >>>>+ >>>>>>>>+ <<<<<<<< <<<< <-]
>>>>>>>> >>>>>>>> >>+<< <<<<<<<< <<<<<<<< <<<<<<<< <<<<<<<<

[
  { UPDATE BALL }
  >>>>>>>>
    >>
      { if b_py == 0, b_vy = b_s }
      + >[<[-]>>+<-] >[<+>-]<<
      [ <<<[+] <<<<<<[>+>>>>>+<<<<<<-] >[<+>-] >>>>> >>>- ]

      { if b_py + b_l == 0, b_vy = -b_s }
      >>>>>[<+<<<+>>>>-]<<<<<
      + >[<[-]>>+<-] >[<+>-]<<
      [ <<<[-] <<<<<<[>+>>>>>-<<<<<<-] >[<+>-] >>>>> >>>- ]
      >>>>[>+<<<<->>>-]<<<<
    <<

    <<<<+>>>>
    <<+>>
    <<<<<++>>>>>

    { A1: b_px == pl_px + p_lx }
    >>>>>>>> >>>>>[<+<<<+>>>>-] <<<<[<+<<<<<<<->>>>>>>>-] <<<<<<<<
    [<<<<<-<->>>>>>[<+>-]] <[>+<-]
    >>>>>>>> [<<<<<<<+>>>>>>>>+<-] >>>>[>+<<<<->>>-] <<<< <<<<<<<<

    { B1: b_px + b_l == pr_px }
    >>>>>>>> >>>>>>>> >[<- <<<<<<<< <<<+>>> >>>>>>>> >+]
    < <<<<<<<< <<<[<+<<<+>>>>-] <<<<
    [<<<-<<<->>>>>>[<+>-]] <[>+<-]
    >>>>[<<<->>>>+<-] >>>> >>>>>>>>
    [<<<<<<<< <<<->>> >>>>>>>> >-< +] <<<<<<<< <<<<<<<<

    <<<<<[[-]>>>>>
      >>>>>>>[<+<<<+>>>>-]<<<<-

        { A2: b_py + b_l - 1 >= pl_py }
        >>>>>>>>
          [ <<<<<<<<
            <+> [<[-]>>+<-] >[<+>-]<
            <[<<<<<<[-]>>>>>>-]>
            ->>>>>>>+>- ]
          <[>+<<<<<<<<+>>>>>>>-]>
        <<<<<<<<

        { B2: b_py + b_l - 1 >= pr_py }
        >>>>>>>> >>>>>>>>
          [ <<<<<<<< <<<<<<<<
            <+> [<[-]>>+<-] >[<+>-]<
            <[<<<<[-]>>>>-]>
            ->>>>>>>> >>>>>>>+>- ]
          <[>+<<<<<<<< <<<<<<<<+>>>>>>>> >>>>>>>-]>
        <<<<<<<< <<<<<<<<

      +>>>[<<<->>>>+<-]<<<<<<

      { A3: b_py <= pl_py + p_ly - 1 }
      >>>>>>>> >>>>>>>[<+<<<+>>>>-] <<<<- <<<<<<<<
        [ <+> >>>>>>>>[<<<<<<<<<->>>>>>>>>[<+>-]] <[>+<-]>
          <<<<<<<<<[<<<<<<[-]>>>>>>-]>>>>>>>>>
          -<<<<<<<+<- ]
        >[<+>>>>>>>>+<<<<<<<-]<
      >>>>>>>>+ >>>[<<<->>>>+<-] <<<<<< <<<<<<<<

      { B3: b_py <= pr_py + p_ry - 1 }
      >>>>>>>> >>>>>>>> >>>>>>>[<+<<<+>>>>-] <<<<- <<<<<<<< <<<<<<<<
        [ <+>
          >>>>>>>> >>>>>>>>[<<<<<<<< <<<<<<<<<->>>>>>>>> >>>>>>>>[<+>-]]
          <[>+<-]>
          <<<<<<<< <<<<<<<<<[<<<<[-]>>>>-]>>>>>>>>> >>>>>>>>
          -<<<<<<<< <<<<<<<+<- ]
        >[<+>>>>>>>> >>>>>>>>+<<<<<<<< <<<<<<<-]<
      >>>>>>>> >>>>>>>>+ >>>[<<<->>>>+<-] <<<<<< <<<<<<<< <<<<<<<<

      { if A1, A2, and A3, b_vx = b_s }
      <<<<[ >[+] <<<<[>+>>>+<<<<-] >[<+>-] >>> <- ]>>>>

      { if B1, B2, and B3, b_vx = -b_s }
      <<[ <[-] <<<<[>+>>>-<<<<-] >[<+>-] >>> >- ]>>
    <<<<<]>>>>>

    { if b_px == 0, run = 0, won = 2 }
    + >[<[-]>>+<-] >[<+>-]<<
    [<<<<<<<<[-] >[-]++ >>>>>>>-]

    { if b_px + b_l == 0, run = 0, won = 1 }
    >>>>>[<+<<<+>>>>-]<<<<<
    + >[<[-]>>+<-] >[<+>-]<<
    [<<<<<<<<[-] >[-]+ >>>>>>>-]
    >>>>[<<<->>>>+<-]<<<<
  <<<<<<<<

  { integrate }
  >>>>>
    [>+>>>+<<<<-] >[<+>-]< >>
    [>+>>>+<<<<-] >[<+>-]< <<
  <<<<<

  { UPDATE PADDLES }
  >>>>>>>> >>>>>>>>
    { read input }
    ;[
      >>++++++++++[<<-------->>-]<<---
      >>+<<[----[<<++++++++++[>>-----<<-]>>+[-[ [-]>>-<<
      ]>>[-<<
        >>>>>>>> >>>>+<<<< <<<<<<<<
      >>]<<]>>[-<<
        >>>>>>>> >>>>-<<<< <<<<<<<<
      >>]<<]>>[-<<
        >>>>+<<<<
      >>]<<]>>[-<<
        >>>>-<<<<
      >>]<<
    ;]

    >>-[+<<
      { if p*_py == 0 and p*_vy == -1, p*_vy = 0 }
      +
      >>>[<<<[-]>>+>-] <[>+<-] <<
      >>>>+[<<<<[-]>>+>>-]- <<[>>+<<-] <<
      [>>>>[-]<<<<-]

      { if p*_py + p_ly == 0 and p*_vy == 1, p*_vy = 0 }
      +
      >>> >>>>[<<<<+>>>+>-]<<<<
      [<<<[-]>>+>-] <[>+<-]
      >>>>[>+<<<<->>>-] <<<<<<
      >>>>-[<<<<[-]>>->>+]+ <<[>>-<<+] <<
      [>>>>[-]<<<<-]

      >>>>>>>>
    >>-]+<<
    <<<<<<<< <<<<<<<<

    {integrate}
    >>>>[<++>-]<<<<
    >>>>>>>> >>>>[<++>-]<<<< <<<<<<<<
  <<<<<<<< <<<<<<<<

  { RENDER }
  { for color = 255, 0 }
  >> --[+
    [>->>>>>-<<<<<<+] ++ >>>>>>

    { for each object }
    >>-[+<<

      { draw rectangle:  clr x0 _ y0 _ w _ h _ }
      [>>>>-<<<<+] >[r <+>-] >>[u <+>-] >
      >>>
        [ <<[<'r> >+<-] >[l <+>-] > u >+<-]
        >[d <+>-]<
      <<<
      <<[d >+<-] <<[l >+<-] >>>>[<<<<->>>>+]<<<<

      [>>>>>>>>-<<<<<<<<+] >>>>>>>>
    >>-]+<<

    [+] >> --[++<< <<<<<<<< >>--] >[<->+]<

    { if color != 0, display }
    [>->-<<+] >>[<<->>+]< [:[+]]<
  ] <<
]

{ PRINT WINNER }
>>
  >++++++++++[<++++++++>-]<.
  >+++++++[<++++>-]<.
  -----------.
  >++++++[<++++>-]<.
  >+++++[<---->-]<.
  +++++++++++++.
  [-]>++++++++[<++++>-]<.
  >++++[<++++>-]<<[>+<-]>.
  [-]>++++++++[<++++>-]<.
  >++++++++++[<+++++++++>-]<---.
  >+++[<----->-]<+.
  +++++.
  +++++.
  [-]>++++++++[<++++>-]<+.
  [-]++++++++++.
<<
